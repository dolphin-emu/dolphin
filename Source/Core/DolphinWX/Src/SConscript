# -*- python -*-

Import('env')
import os
import sys

files = [
	'BootManager.cpp',
	]

libs = [
	'core', 'lzo2', 'discio', 'bdisasm',
	'inputcommon', 'common', 'lua', 'z', 'sfml-network',
	]

if env['HAVE_WX']:
	memcardfiles = [
		'MemcardManager.cpp',
		'MemoryCards/GCMemcard.cpp',
		'WxUtils.cpp',
		]

	files += [
		'AboutDolphin.cpp',
		'ARCodeAddEdit.cpp',
		'GeckoCodeDiag.cpp',
		'ConfigMain.cpp',
		'Frame.cpp',
		'FrameAui.cpp',
		'FrameTools.cpp',
		'LuaWindow.cpp',
		'LogWindow.cpp',
		'GameListCtrl.cpp',
		'HotkeyDlg.cpp',
		'ISOFile.cpp',
		'ISOProperties.cpp',
		'PatchAddEdit.cpp',
		'CheatsWindow.cpp',
		'Main.cpp',
		'MemoryCards/WiiSaveCrypted.cpp',
		'NetPlay.cpp',
		'NetPlayClient.cpp',
		'NetPlayServer.cpp',
		'NetWindow.cpp',
		]

	wxlibs = [ 'debwx', 'debugger_ui_util', 'inputuicommon', 'memcard' ]
else:
	files+= [
		'MainNoGUI.cpp',
		'cmdline.c',
		]

if sys.platform == 'win32':
	files += [ "stdafx.cpp" ]
elif sys.platform == 'darwin' and not env['HAVE_WX']:
	files += [ 'cocoaApp.m' ]
	exeNoGUI = env['binary_dir'] + 'DolphinNoGUI'
elif sys.platform == 'darwin' and env['HAVE_WX']:
	exeGUI = 'Dolphin'
	exeGUIdist = env['binary_dir'] + 'Dolphin.app/Contents/MacOS/Dolphin'

	env.Install(env['binary_dir'] + 'Dolphin.app/Contents/' +
		'Library/Frameworks/Cg.framework',
		'#Externals/Cg/Cg.framework/Cg')

	env.Install(env['data_dir'],
		'#Source/Core/DolphinWX/resources/Dolphin.icns')

	from plistlib import writePlist
	def createPlist(target, source, env):
		for srcNode in source:
			writePlist(srcNode.value, str(target[0]))
	env.Append(BUILDERS = {'Plist' : Builder(action = createPlist)})
	env.Plist(env['binary_dir'] + 'Dolphin.app/Contents/Info.plist', Value(
		dict(
		CFBundleExecutable = 'Dolphin',
		CFBundleIconFile = 'Dolphin.icns',
		CFBundleIdentifier = 'com.dolphin-emu.dolphin',
		CFBundlePackageType = 'APPL',
		CFBundleShortVersionString =
			os.popen('svnversion -n ' + Dir('#').abspath).read(),
		CFBundleVersion = '2.0',
		LSMinimumSystemVersion = '10.5.0',
		LSRequiresCarbon = True,
		)))

	def Fixup(target, source, env):
		os.link(str(source[0]), str(target[0]))
	env.Append(BUILDERS = {'Fixup' : Builder(action = Fixup)})
	env.Depends(exeGUIdist, env['plugin_dir'])
	if env['CPPDEFINES'].count('WXUSINGDLL'):
		env.Fixup(exeGUIdist, exeGUI)
else:
	files += [ 'X11Utils.cpp' ]
	libs += [ 'SDL' ]
	exeGUI = env['binary_dir'] + 'dolphin-emu'
	exeNoGUI = env['binary_dir'] + 'dolphin-emu-nogui'

if env['HAVE_WX']:
	env.StaticLibrary(env['local_libs'] + 'memcard', memcardfiles)
	env.Program(exeGUI, files, LIBS = env['LIBS'] + wxlibs + libs)
else:
	env.Program(exeNoGUI, files, LIBS = env['LIBS'] + libs)
