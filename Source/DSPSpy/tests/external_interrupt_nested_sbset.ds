; This test needs to manually specify IRQs
	jmp		irq0
	jmp		irq1
	jmp		irq2
	jmp		irq3
	jmp		irq4
	jmp		irq5
	jmp		irq6
	jmp		external_irq

incdir  "tests"
include "dsp_base_noirq.inc"

test_main:
	CLR $ACC0
	CLR $ACC1
	SBCLR #2
	SBCLR #3
	SBCLR #4
	SBCLR #5
	SBCLR #6

	LRI $AR0, #0

	LRIS $AX0.H, #1
	CALL send_back

	SI @DMBH, #0x8888
	SI @DMBL, #0x5371

wait_cpu_read:
	LRS $AC1.M, @DMBH
	ANDCF $AC1.M, #0x8000
	JLZ wait_cpu_read

	CLR $ACC1
second_loop:
	INC $ACC1
	CMPIS $AC1.M, #1
	JNZ second_loop

	SBSET #5
	SBSET #6
	LRI $AR0, #2
	LRI $AR0, #3
	LRI $AR0, #4
	LRI $AR0, #5
	LRI $AR0, #6

	LRIS $AX0.H, #2
	CALL send_back

	JMP end_of_test

external_irq:
	INC $ACC0
	INCM $AC0.M
	LRIS $AX0.H, #3
	CALL send_back

	; Only trigger a nested interrupt the first time through.
	CMPIS $AC0.M, #2
	JGE exit_irq

	SI @DMBH, #0x8888
	SI @DMBL, #0x5371

wait_cpu_read_irq:
	LRS $AC1.M, @DMBH
	ANDCF $AC1.M, #0x8000
	JLZ wait_cpu_read_irq

	CLR $ACC1
	SBSET #6

second_loop_irq:
	INC $ACC1
	CMPIS $AC1.M, #1
	JNZ second_loop_irq

exit_irq:
	DEC $ACC0
	RTI

; Expected result ($AX0.H (send_back num), $AC0.L (interrupt depth), $AC0.M (interrupt count), $AR0, and $SR):
; 1, 0, 0, 0, 2024
; 3, 1, 1, 2, 2820
; 3, 2, 2, 2, 2820
; 2, 0, 2, 6, 3825
