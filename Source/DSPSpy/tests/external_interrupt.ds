; This test needs to manually specify IRQs
	jmp		irq0
	jmp		irq1
	jmp		irq2
	jmp		irq3
	jmp		irq4
	jmp		irq5
	jmp		irq6
	jmp		external_irq

incdir  "tests"
include "dsp_base_noirq.inc"

test_main:
	CLR $ACC0
	CLR $ACC1
	SBCLR #2
	SBCLR #3
	SBCLR #4
	SBSET #5
	SBSET #6

	LRIS $AX0.H, #1
	CALL send_back

	SI @DMBH, #0x8888
	SI @DMBL, #0x5371

wait_external_irq:
	INC $ACC1
	TST $ACC0
	JZ wait_external_irq

	LRIS $AX0.H, #2
	CALL send_back

	JMP end_of_test

external_irq:
	INC $ACC0
	LRIS $AX0.H, #3
	CALL send_back
	RTI

; Expected output ($AX0.H (send_back num), $AC0.L (interrupt count), and $SR):
; 1, 0, 3824 (start)
; 3, 1, 2820 (in interrupt handler)
; 2, 1, 3820 (back out of interrupt handler)
; DSPSpy shows "interrupt after 20 ticks / 0 tries" (exact number of ticks varies)
; ACC1 is 7bb40 or so (also varies, and can vary between steps 2 and 3 if the interrupt happens on the JZ)
